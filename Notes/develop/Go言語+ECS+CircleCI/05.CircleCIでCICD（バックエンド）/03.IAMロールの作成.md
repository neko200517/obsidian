## CircleCI用にOIDC認証する

## IDプロバイダを追加

1. IAM→[アクセス管理]IDプロバイダ→プロバイダを追加
2. OpenID Connectを選択
3. プロバイダのURLを入力
4. 対象者を入力 
5. プロバイダを追加を押下

https://zenn.dev/kou_pg_0131/articles/circleci-oidc-aws

## フォルダ構成

```json
circlecd
├── main.tf
├── modules
│   └── iam
│       ├── iam-role-ecr.tf
│       ├── iam-role-ecs.tf
│       ├── id-provider.tf
│       └── variables.tf
├── provider.tf
├── terraform.tfstate
├── terraform.tfstate.backup
└── variables.tf
```

## メイン
### main.tf

```json
module "iam" {
  source = "./modules/iam"
}
```

### variables.tf

```json
variable "aws_region" {
  default = "ap-northeast-1"
}
```

### provider.tf 

```json
provider "aws" {
  region = var.aws_region
}
```

## モジュール (IAM)

- IAM Policyは公式資料が整っていないので簡易的にFullAccessを与えている

```json
## CircleCI

variable "circleci_organization_id" {
  default = "{CircleCIで作成した組織ID}"
}

variable "circleci_project_id" {
  default = "{CircleCIで作成したプロジェクトID}"
}
```

### modules/iam/id-provider.tf 

```json
## ID Provider

data "http" "circleci_openid_configuration" {
  url = "https://oidc.circleci.com/org/${var.circleci_organization_id}/.well-known/openid-configuration"
}

data "tls_certificate" "circleci" {
  url = jsondecode(data.http.circleci_openid_configuration.response_body).jwks_uri
}

resource "aws_iam_openid_connect_provider" "circleci" {
  url             = "https://oidc.circleci.com/org/${var.circleci_organization_id}"
  client_id_list  = [var.circleci_organization_id]
  thumbprint_list = data.tls_certificate.circleci.certificates[*].sha1_fingerprint
}
```

### modules/iam/iam-role-ecr.tf 

```json
## IAM Role (ECR)
data "aws_iam_policy_document" "circleci_assume_role_policy_ecr" {
  statement {
    effect  = "Allow"
    actions = ["sts:AssumeRoleWithWebIdentity"]
    principals {
      type        = "Federated"
      identifiers = [aws_iam_openid_connect_provider.circleci.arn]
    }
    condition {
      test     = "StringLike"
      variable = "oidc.circleci.com/org/${var.circleci_organization_id}:sub"
      values   = ["org/${var.circleci_organization_id}/project/${var.circleci_project_id}/user/*"]
    }
  }
}

resource "aws_iam_role" "circleci_ecr" {
  name               = "circleci-oidc-role-my-go-api-ecr"
  assume_role_policy = data.aws_iam_policy_document.circleci_assume_role_policy_ecr.json
}

## IAM Policy - AmazonEC2ContainerRegistryFullAccess
resource "aws_iam_role_policy_attachment" "circleci_role_policy_ecr_full" {
  role       = aws_iam_role.circleci_ecr.name
  policy_arn = "arn:aws:iam::aws:policy/AmazonEC2ContainerRegistryFullAccess"
}
```

### modules/iam/iam-role-ecs.tf

```json
## IAM Role (ECS)
data "aws_iam_policy_document" "circleci_assume_role_policy_ecs" {
  statement {
    effect  = "Allow"
    actions = ["sts:AssumeRoleWithWebIdentity"]
    principals {
      type        = "Federated"
      identifiers = [aws_iam_openid_connect_provider.circleci.arn]
    }
    condition {
      test     = "StringLike"
      variable = "oidc.circleci.com/org/${var.circleci_organization_id}:sub"
      values   = ["org/${var.circleci_organization_id}/project/${var.circleci_project_id}/user/*"]
    }
  }
}

resource "aws_iam_role" "circleci_ecs" {
  name               = "circleci-oidc-role-my-go-api-ecs"
  assume_role_policy = data.aws_iam_policy_document.circleci_assume_role_policy_ecs.json
}

## IAM Policy - AmazonECS_FullAccess
resource "aws_iam_role_policy_attachment" "circleci_role_policy_ecs_full" {
  role       = aws_iam_role.circleci_ecs.name
  policy_arn = "arn:aws:iam::aws:policy/AmazonECS_FullAccess"
}
```
