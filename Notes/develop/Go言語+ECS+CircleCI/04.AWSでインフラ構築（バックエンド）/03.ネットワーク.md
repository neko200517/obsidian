## VPC

- 基本的なVPCの構成

### vpc.tf

```json
## VPC
resource "aws_vpc" "my_vpc" {
  cidr_block = "10.0.0.0/16"

  tags = {
    Name = "my-go-api-my_vpc"
  }
}
```

## サブネット

- パブリックサブネットとプライベートサブネット、それぞれ2つのAZごとに構築

### subnet.tf

```json
## Public Subnet (ALB)
resource "aws_subnet" "public" {
  count             = 2
  vpc_id            = aws_vpc.my_vpc.id
  cidr_block        = "10.0.${count.index + 2}.0/24"
  map_public_ip_on_launch = true
  availability_zone = element(["ap-northeast-1a", "ap-northeast-1c"], count.index)

  tags = {
    Name = "my-go-api-public-${count.index + 2}"
  }
}

## Private Subnet (ECS)
resource "aws_subnet" "private" {
  count             = 2
  vpc_id            = aws_vpc.my_vpc.id
  cidr_block        = "10.0.${count.index}.0/24"
  map_public_ip_on_launch = false
  availability_zone = element(["ap-northeast-1a", "ap-northeast-1c"], count.index)

  tags = {
    Name = "my-go-api-private-${count.index}"
  }
}
```

## ルートテーブル

- パブリックサブネット、プライベートサブネット共に0.0.0.0/0を開放
- プライベートサブネットはNATゲートウェイ経由で外部接続
	- ECSにデプロイするにはDockerをビルドする必要があり、その際にイメージをダウンロードするためインターネット環境が必要なため

### route-table.tf

```json
## Route Table (ALB)
resource "aws_route_table" "public_rt" {
  vpc_id = aws_vpc.my_vpc.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.igw.id
  }

  tags = {
    Name = "my-go-api-public-rt"
  }
}

resource "aws_route_table_association" "public_association" {
  count          = length(aws_subnet.public[*].id)
  subnet_id      = element(aws_subnet.public[*].id, count.index)
  route_table_id = aws_route_table.public_rt.id
}

## Route Table (ECS)
resource "aws_route_table" "private_rt" {
  vpc_id = aws_vpc.my_vpc.id

  route {
    cidr_block = "0.0.0.0/0"
    nat_gateway_id = element(aws_nat_gateway.nat[*].id, 0)
  }

  tags = {
    Name = "my-go-api-private-rt"
  }
}

resource "aws_route_table_association" "private_association" {
  count          = length(aws_subnet.private[*].id)
  subnet_id      = element(aws_subnet.private[*].id, count.index)
  route_table_id = aws_route_table.private_rt.id
}
```

## インターネットゲートウェイ

- パブリックサブネット、NATゲートウェイに割り当てるインターネットゲートウェイ

```json
## Internet Gateway
resource "aws_internet_gateway" "igw" {
  vpc_id = aws_vpc.my_vpc.id

  tags = {
    Name = "my-go-api-igw"
  }
}
```

## NATゲートウェイ

- インターネットゲートウェイに割り当てて外部接続可能にする

### nat-gateway.tf

```json
## Nat Gateway (Private Subnet -> Internet)
resource "aws_nat_gateway" "nat" {
  count         = length(aws_subnet.public[*].id)
  allocation_id = aws_eip.nat[count.index].id
  subnet_id     = element(aws_subnet.public[*].id, count.index)

  tags = {
    Name = "my-go-api-nat"
  }
}

## EIP
resource "aws_eip" "nat" {
  count = length(aws_subnet.public[*].id)

  tags = {
    Name = "my-go-api-nat-eip"
  }
}
```

## セキュリティグループ

- パブリックサブネット（ALB用）はインバウンドにALBは80ポートを開放
- プライベートサブネット（ECS用）はインバウンドにALB経由で8080を開放

### security-group.tf 

```json
## Security Group (ALB)
resource "aws_security_group" "alb_sg" {
  vpc_id = aws_vpc.my_vpc.id

  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "my-go-api-alb-sg"
  }
}

## Security Group (ECS)
resource "aws_security_group" "my_sg" {
  vpc_id = aws_vpc.my_vpc.id

  ingress {
    from_port   = 8080
    to_port     = 8080
    protocol    = "tcp"
    security_groups = [aws_security_group.alb_sg.id]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "my-go-api-my-sg"
  }
}
```

## VPCエンドポイント

- Fargate 1.4.0 からエンドポイントの追加が必要になったためエンドポイントを追加
- その他、CloudWatchにログを出力する場合にも別途エンドポイントが必要

### vpc-endpoint.tf

```json
## VPC Endpoint
resource "aws_vpc_endpoint" "ecr_api" {
  vpc_id            = aws_vpc.my_vpc.id
  service_name      = "com.amazonaws.${var.aws_region}.ecr.api"
  vpc_endpoint_type = "Interface"
  security_group_ids = [aws_security_group.my_sg.id]
  subnet_ids        = aws_subnet.private[*].id

  tags = {
    Name = "my-go-api-ecr-api"
  }
}

resource "aws_vpc_endpoint" "ecr_dkr" {
  vpc_id            = aws_vpc.my_vpc.id
  service_name      = "com.amazonaws.${var.aws_region}.ecr.dkr"
  vpc_endpoint_type = "Interface"
  security_group_ids = [aws_security_group.my_sg.id]
  subnet_ids        = aws_subnet.private[*].id

  tags = {
    Name = "my-go-api-ecr-dkr"
  }
}

resource "aws_vpc_endpoint" "s3" {
  vpc_id       = aws_vpc.my_vpc.id
  service_name = "com.amazonaws.${var.aws_region}.s3"
  vpc_endpoint_type = "Gateway"

  tags = {
    Name = "my-go-api-s3"
  }
}
```

## ロードバランサー

- プライベートサブネットのECSにアクセスするためにALBを仲介する

### alb.tf

```json
## ALB
resource "aws_lb" "my_alb" {
  name               = "my-app-alb"
  internal           = false
  load_balancer_type = "application"
  security_groups    = [aws_security_group.alb_sg.id]
  subnets            = aws_subnet.public[*].id
}

## ALB Target Group
resource "aws_lb_target_group" "my_tg" {
  name        = "my-target-group"
  port        = 8080
  protocol    = "HTTP"
  vpc_id      = aws_vpc.my_vpc.id
  target_type = "ip"
}

## ALB Listener
resource "aws_lb_listener" "http_listener" {
  load_balancer_arn = aws_lb.my_alb.arn
  port              = 80
  protocol          = "HTTP"
  default_action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.my_tg.arn
  }
}
```