## 変数、出力

### modules/compute/variables.tf

```json
variable "project_name" {
  type = string
}

variable "vpc_id" {
  type = string
}

variable "ecs_exec_role" {
  type = string
}

variable "private_subnets" {
  type = list(string)
}

variable "ecs_sg" {
  type = string
}

variable "ecs_tg" {
  type = string
}

variable "container_port" {
  default = 8080
}

variable "cpu" {
  default = 256
}

variable "memory" {
  default = 512
}

variable "bastion_sg_id" {
  type = string
}

variable "ami_id" {
  type = string
}

variable "public_subnet" {
  type = string
}
```

## ECR / ECS

### modules/compute/ecr.tf 

```json
## ECR
resource "aws_ecr_repository" "my_ecr" {
  name = "${var.project_name}-ecr"
}
```

### modules/compute/ecs.tf 

- Fargate

```json
## ECS Cluster
resource "aws_ecs_cluster" "my_cluster" {
  name = "${var.project_name}-cluster"
}

## ECS Task Definition
resource "aws_ecs_task_definition" "my_task" {
  family                   = "${var.project_name}-task"
  network_mode             = "awsvpc"
  requires_compatibilities = ["FARGATE"]
  execution_role_arn       = var.ecs_exec_role
  cpu                      = var.cpu
  memory                   = var.memory

  container_definitions = jsonencode([
    {
      name      = "${var.project_name}-container"
      image     = "${aws_ecr_repository.my_ecr.repository_url}:latest"
      cpu       = var.cpu
      memory    = var.memory
      essential = true
      portMappings = [
        {
          containerPort = var.container_port
          hostPort      = var.container_port
        }
      ]
    }
  ])
}

## ECS Service
resource "aws_ecs_service" "my_service" {
  name            = "${var.project_name}-service"
  cluster         = aws_ecs_cluster.my_cluster.id
  task_definition = aws_ecs_task_definition.my_task.arn
  launch_type     = "FARGATE"

  network_configuration {
    subnets          = var.private_subnets[*]
    security_groups  = [var.ecs_sg]
    assign_public_ip = false
  }

  desired_count = 1
  load_balancer {
    target_group_arn = var.ecs_tg
    container_name   = "${var.project_name}-container"
    container_port   = var.container_port
  }
}
```

## Bastin (踏み台)

- RDSに接続するために使用する。SSMの権限を与えることで接続可
- 無料利用枠の最安プランを使用することでコストを最小限に抑える

```json
## RDS用の踏み台サーバ
resource "aws_instance" "bastion" {
  ami                         = var.ami_id
  instance_type               = "t3.micro" # ✅ 最安（t4g.microはarm系）
  subnet_id                   = var.public_subnet
  vpc_security_group_ids      = [var.bastion_sg_id]
  associate_public_ip_address = true
  iam_instance_profile        = aws_iam_instance_profile.ssm_profile.name

  tags = {
    Name = "${var.project_name}-bastion"
  }
}

resource "aws_iam_role" "ssm_role" {
  name = "${var.project_name}-bastion-ssm-role"

  assume_role_policy = jsonencode({
    Version = "2012-10-17",
    Statement = [{
      Effect = "Allow",
      Principal = {
        Service = "ec2.amazonaws.com"
      },
      Action = "sts:AssumeRole"
    }]
  })
}

resource "aws_iam_role_policy_attachment" "ssm_core" {
  role       = aws_iam_role.ssm_role.name
  policy_arn = "arn:aws:iam::aws:policy/AmazonSSMManagedInstanceCore"
}

resource "aws_iam_instance_profile" "ssm_profile" {
  name = "${var.project_name}-bastion-ssm-profile"
  role = aws_iam_role.ssm_role.name
}
```