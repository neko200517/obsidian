## 変数

### modules/load-balancer/variables.tf

```json
variable "project_name" {
  type = string
}

## Network
variable "vpc_id" {
  type = string
}

variable "public_subnets" {
  type = list(string)
}

## Load Balancer
variable "alb_sg" {
  type = string
}

## Storage
variable "s3_bucket_name" {
  type = string
}

variable "s3_bucket_id" {
  type = string
}

variable "s3_bucket_domain_name" {
  type = string
}

## ACM
variable "cloudfront_domain_name" {
  type = string
}

variable "cloudfront_cert_arn" {
  type = string
}

variable "alb_cert_arn" {
  type = string
}

```

## 出力

### modules/load-balancer/outputs.tf 

```json
output "ecs_tg" {
  value = aws_lb_target_group.ecs_tg.arn
}

output "cf_dist" {
  value = aws_cloudfront_distribution.main.arn
}

## ACM
output "cloudfront_dns_name" {
  value = aws_cloudfront_distribution.main.domain_name
}

output "cloudfront_zone_id" {
  value = aws_cloudfront_distribution.main.hosted_zone_id
}

output "alb_dns_name" {
  value = aws_lb.my_alb.dns_name
}

output "alb_zone_id" {
  value = aws_lb.my_alb.zone_id
}

output "cloudfront_distribution_id" {
  value = aws_cloudfront_distribution.main.id
}
```

## ロードバランサー

- プライベートサブネットのECSにアクセスするためにALBを仲介する

### modules/load-balancer/alb.tf

```json
## ALB
resource "aws_lb" "my_alb" {
  name               = "${var.project_name}-alb"
  internal           = false
  load_balancer_type = "application"
  security_groups    = [var.alb_sg]
  subnets            = var.public_subnets[*]
}

## ALB Target Group
resource "aws_lb_target_group" "ecs_tg" {
  name        = "${var.project_name}-tg"
  port        = 8080
  protocol    = "HTTP"
  vpc_id      = var.vpc_id
  target_type = "ip"
}

## ALB Listener
resource "aws_lb_listener" "https" {
  load_balancer_arn = aws_lb.my_alb.arn
  port              = 443
  protocol          = "HTTPS"
  ssl_policy        = "ELBSecurityPolicy-TLS13-1-2-2021-06"
  certificate_arn   = var.alb_cert_arn
  
  default_action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.ecs_tg.arn
  }
}
```

## Cloud Front 

### modules/load-balancer/cloudfront.tf 

```json
 resource "aws_cloudfront_distribution" "main" {
   origin {
    origin_id   = var.s3_bucket_id
    domain_name = var.s3_bucket_domain_name
    origin_access_control_id = aws_cloudfront_origin_access_control.main.id
   }

   default_cache_behavior {
     target_origin_id       = var.s3_bucket_id
     viewer_protocol_policy = "redirect-to-https"
     cached_methods         = ["GET", "HEAD"]
     allowed_methods        = ["GET", "HEAD"]
     forwarded_values {
       query_string = false
       headers      = []
       cookies {
         forward = "none"
       }
     }
   }

   enabled = true
   default_root_object = "index.html"
   aliases = [var.cloudfront_domain_name]

   viewer_certificate {
     acm_certificate_arn      = var.cloudfront_cert_arn
     ssl_support_method       = "sni-only"
     minimum_protocol_version = "TLSv1.2_2021"
   }

   restrictions {
     geo_restriction {
       restriction_type = "none"
     }
   }
 }

 # OAC を作成
resource "aws_cloudfront_origin_access_control" "main" {
  name                              = "${var.project_name}-cf-oac"
  origin_access_control_origin_type = "s3"
  signing_behavior                  = "always"
  signing_protocol                  = "sigv4"
}
```