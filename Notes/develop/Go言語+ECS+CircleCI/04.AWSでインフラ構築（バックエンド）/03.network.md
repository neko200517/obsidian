## 変数

### modules/network/variables.tf 

```json
variable "project_name" {
  type = string
}

variable "aws_region" {
  default = "ap-northeast-1"
}

variable "vcp_cidr_block" {
  type = string
}

variable "ecs_sg" {
  type = string
}

variable "private_link_sg" {
  type = string
}

variable "enable_nat_gateway" {
  type = bool
}

variable "enable_vpc_endpoint" {
  type = bool
}
```

## 出力 

### modules/network/outputs.tf

```json
output "vpc_id" {
  value = aws_vpc.my_vpc.id
}

output "public_subnets" {
  value = aws_subnet.public[*].id
}

output "private_subnets" {
  value = aws_subnet.private[*].id
}
```

## VPC

- 基本的なVPCの構成

### modules/network/vpc.tf

```json
## VPC
resource "aws_vpc" "my_vpc" {
  cidr_block = "10.0.0.0/16"
  enable_dns_hostnames = true

  tags = {
    Name = "${var.project_name}-my_vpc"
  }
}
```

## サブネット

- パブリックサブネットとプライベートサブネット、それぞれ2つのAZごとに構築

### modules/network/subnet.tf

```json
## Public Subnet (ALB)
resource "aws_subnet" "public" {
  count             = 2
  vpc_id            = aws_vpc.my_vpc.id
  cidr_block        = "10.0.${count.index + 2}.0/24"
  map_public_ip_on_launch = true
  availability_zone = element(["ap-northeast-1a", "ap-northeast-1c"], count.index)

  tags = {
    Name = "${var.project_name}-public-${count.index + 2}"
  }
}

## Private Subnet (ECS)
resource "aws_subnet" "private" {
  count             = 2
  vpc_id            = aws_vpc.my_vpc.id
  cidr_block        = "10.0.${count.index}.0/24"
  map_public_ip_on_launch = false
  availability_zone = element(["ap-northeast-1a", "ap-northeast-1c"], count.index)

  tags = {
    Name = "${var.project_name}-private-${count.index}"
  }
}
```

## ルートテーブル

- パブリックサブネット、プライベートサブネット共に0.0.0.0/0を開放
- プライベートサブネットはNATゲートウェイ経由で外部接続
	- ECSにデプロイするにはDockerをビルドする必要があり、その際にイメージをダウンロードするためインターネット環境が必要なため

### modules/network/route-table.tf

```json
## Route Table (ALB)
resource "aws_route_table" "public_rt" {
  vpc_id = aws_vpc.my_vpc.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.igw.id
  }

  tags = {
    Name = "${var.project_name}-public-rt"
  }
}

resource "aws_route_table_association" "public_association" {
  count          = length(aws_subnet.public[*].id)
  subnet_id      = element(aws_subnet.public[*].id, count.index)
  route_table_id = aws_route_table.public_rt.id
}

## Route Table (ECS)
resource "aws_route_table" "private_rt" {
  vpc_id = aws_vpc.my_vpc.id

  dynamic "route" {
    for_each = var.enable_nat_gateway ? [1] : [] # ✅ NAT Gateway 有効時のみルートを作成
    content {
      cidr_block     = "0.0.0.0/0"
      nat_gateway_id = aws_nat_gateway.nat[0].id
    }
  }

  tags = {
    Name = "${var.project_name}-private-rt"
  }
}

resource "aws_route_table_association" "private_association" {
  count          = length(aws_subnet.private[*].id)
  subnet_id      = element(aws_subnet.private[*].id, count.index)
  route_table_id = aws_route_table.private_rt.id
}
```

## インターネットゲートウェイ

- パブリックサブネット、NATゲートウェイに割り当てるインターネットゲートウェイ

### modules/network/internet-gateway.tf

```json
## Internet Gateway
resource "aws_internet_gateway" "igw" {
  vpc_id = aws_vpc.my_vpc.id

  tags = {
    Name = "${var.project_name}-igw"
  }
}
```

## NATゲートウェイ

- インターネットゲートウェイに割り当てて外部接続可能にする
- サブネットの1つだけに適用してコストを削減する
- count追加: falseの場合削除 (コスト削減)

### modules/network/nat-gateway.tf

```json
## EIP
resource "aws_eip" "nat" {
  count = var.enable_nat_gateway ? 1 : 0

  tags = {
    Name = "${var.project_name}-nat-eip"
  }
}

## Nat Gateway (Private Subnet -> Internet)
resource "aws_nat_gateway" "nat" {
  count = var.enable_nat_gateway ? 1 : 0

  allocation_id = aws_eip.nat[0].id
  subnet_id     = element(aws_subnet.public[*].id, 0)

  tags = {
    Name = "${var.project_name}-nat"
  }
}
```

## VPCエンドポイント

- NAT Gateway経由でなくてもECSがECRをデプロイするために必要
- countを追加: falseの場合削除する (コスト削減)

### modules/network/vpc-endpoint.tf

```json
## VPC Endpoint
resource "aws_vpc_endpoint" "ecr_api" {
  count             = var.enable_vpc_endpoint ? 1 : 0
  vpc_id            = aws_vpc.my_vpc.id
  service_name      = "com.amazonaws.${var.aws_region}.ecr.api"
  vpc_endpoint_type = "Interface"
  security_group_ids = [var.private_link_sg]
  subnet_ids        = aws_subnet.private[*].id
  private_dns_enabled = true

  tags = {
    Name = "${var.project_name}-ecr-api"
  }
}

resource "aws_vpc_endpoint" "ecr_dkr" {
  count             = var.enable_vpc_endpoint ? 1 : 0
  vpc_id            = aws_vpc.my_vpc.id
  service_name      = "com.amazonaws.${var.aws_region}.ecr.dkr"
  vpc_endpoint_type = "Interface"
  security_group_ids = [var.private_link_sg]
  subnet_ids        = aws_subnet.private[*].id
  private_dns_enabled = true

  tags = {
    Name = "${var.project_name}-ecr-dkr"
  }
}

resource "aws_vpc_endpoint" "s3" {
  count        = var.enable_vpc_endpoint ? 1 : 0
  vpc_id       = aws_vpc.my_vpc.id
  service_name = "com.amazonaws.${var.aws_region}.s3"
  vpc_endpoint_type = "Gateway"
  route_table_ids = [aws_route_table.private_rt.id]

  tags = {
    Name = "${var.project_name}-s3"
  }
}
```
